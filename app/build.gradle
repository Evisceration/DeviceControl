apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19

        ndk {
            moduleName 'libDeviceControl'
            stl "gnustl_static"
            abiFilters "armeabi", "armeabi-v7a"
        }

    }
    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            assert localProps['keystore.props.file'];
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }
    buildTypes {
        release {
            runProguard true
            signingConfig signingConfigs.release
            proguardFile 'src/main/proguard.flags'
            zipAlign true
            debuggable false
        }
        debug {
            runProguard true
            signingConfig signingConfigs.release
            proguardFile 'src/main/proguard.flags'
            zipAlign true
        }
    }
    lintOptions {
        abortOnError true
        checkAllWarnings true
        disable 'HardcodedDebugMode', 'ValidFragment', 'MissingTranslation', 'ExtraTranslation'
    }
    dexOptions {
        incremental true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(dir: "$buildDir/native-libs", include: 'native-libs.jar')
    compile project(':proprietary')
    compile 'ch.acra:acra:4.+'
    compile 'com.android.support:support-v13:19.+'
    compile 'com.jakewharton:butterknife:5.+'
    compile 'com.koushikdutta.ion:ion:1.2.+'
}

task nativeLibsToJar(type: Zip, description: 'create a jar archive of the native libs') {
    destinationDir file("$buildDir/native-libs")
    baseName 'native-libs'
    extension 'jar'
    from fileTree(dir: 'libs', include: '**/*.so')
    into 'lib/'
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(nativeLibsToJar)
}

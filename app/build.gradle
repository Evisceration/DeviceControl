apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    signingConfigs {
        release {
            def String localProps = System.getenv('SIGNING_PROP_FILE')
            if (localProps == null) {
                logger.quiet('Not signing the build!')
                storeFile file("debug.keystore")
            } else {
                logger.quiet('Signing the build!')
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(file(localProps)))
                storeFile file(keyProps["store"])
                storePassword keyProps["storePass"]
                keyAlias keyProps["alias"]
                keyPassword keyProps["pass"]
            }
        }
    }

    defaultConfig {
        applicationId 'org.namelessrom.devicecontrol'

        def String localProps = System.getenv('SIGNING_PROP_FILE')
        if (localProps == null) {
            signingConfig signingConfigs.debug
        } else {
            signingConfig signingConfigs.release
        }
    }

    buildTypes {
        release {
            debuggable false
            proguardFile 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
        debug {
            debuggable true
            proguardFile 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // do not warn about translations
        disable 'MissingTranslation', 'ExtraTranslation';
    }

    dexOptions {
        incremental false
        javaMaxHeapSize "2048M"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {}

    packagingOptions {
        exclude '.readme'
    }

}

dependencies {
    compile fileTree(dir: '../libs', include: ['*.jar'])

    // Debugging, see https://github.com/square/leakcanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:support-v13:22.2.0'
    compile 'com.google.android.gms:play-services:7.5.0'

    // see https://github.com/Evisceration/AndroidSwipeLayout
    compile('com.github.Evisceration:AndroidSwipeLayout:6544102') {
        exclude group: 'com.google.android', module: 'recyclerview-v7'
        exclude group: 'com.google.android', module: 'support-v4'
    }

    // see https://github.com/Evisceration/logger
    compile 'com.github.Evisceration:logger:1.0.1'

    // see https://github.com/PhilJay/MPAndroidChart
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.0'

    // see https://github.com/koush/ion
    compile 'com.koushikdutta.ion:ion:2.1.6'

    // see https://www.pollfish.com/android
    compile 'com.pollfish:pollfish:4.1.0:googleplayRelease@aar'

    // see https://github.com/pilgr/Paper
    //compile 'io.paperdb:paperdb:0.9'
    compile 'com.github.Evisceration:Paper:6ad14da863'

    // see https://github.com/Evisceration/MaterialPreferences
    // MATERIAL PREFERENCES START
    def String propLocalMaterial = System.getenv('USE_LOCAL_MATERIALPREFERENCES_SOURCES')
    logger.quiet('propLocalMaterial -> ' + propLocalMaterial)
    def boolean useLocalMaterial = (propLocalMaterial != null)

    def String propPlatformBuild = System.getenv('TARGET_PRODUCT')
    logger.quiet('propPlatformBuild -> ' + propPlatformBuild)
    def boolean isPlatformBuild = (propPlatformBuild != null)

    if (!useLocalMaterial || isPlatformBuild) {
    //if (false) {
        logger.quiet('MaterialPrefernces -> release version')
        // the version code equals the release tag OR the shortcode of the commit to use
        compile('com.github.Evisceration:MaterialPreferences:0.1.8') {
            exclude group: 'com.google.android', module: 'appcompat-v7'
            exclude group: 'com.google.android', module: 'cardview-v7'
        }
    } else {
        logger.quiet('MaterialPrefernces -> local sources')
        compile(project(':locallibs:MaterialPreferences:library')) {
            exclude group: 'com.google.android', module: 'appcompat-v7'
            exclude group: 'com.google.android', module: 'cardview-v7'
        }
    }
    // MATERIAL PREFERENCES END
}

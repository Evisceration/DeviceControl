apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    signingConfigs {
        release {
            def String localProps = System.getenv('SIGNING_PROP_FILE')
            if (localProps == null) {
                logger.quiet('Not signing the build!')
            } else {
                logger.quiet('Signing the build, prop file: ' + localProps)
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(file(localProps)))
                storeFile file(keyProps["store"])
                storePassword keyProps["storePass"]
                keyAlias keyProps["alias"]
                keyPassword keyProps["pass"]
            }
        }
    }

    defaultConfig {
        applicationId 'org.namelessrom.devicecontrol'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            debuggable false
            proguardFile 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
        }
        debug {
            debuggable true
            proguardFile 'src/main/proguard.pro'
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
    }

    lintOptions {
        abortOnError true
        checkAllWarnings true
        // do not warn about translations
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    dexOptions {
        incremental false
        javaMaxHeapSize "2048M"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {}
}

dependencies {
    compile fileTree(dir: '../libs', include: ['*.jar'])
    // MPChartLib inherits support-v13
    compile project(':libs:mpandroidchart:MPChartLib')
    // AndroidSwipeLayout inherits recyclerview
    compile project(':libs:AndroidSwipeLayout:library')
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:cardview-v7:22.1.1'
    compile 'com.github.Evisceration:logger:1.0.1'
    compile 'com.google.android.gms:play-services:7.3.0'
    compile 'com.koushikdutta.ion:ion:2.1.3'

    def String localProps = System.getenv('USE_LOCAL_MATERIALPREFERENCES_SOURCES')
    if (localProps == null) {
        // the version code equals the release tag OR the shortcode of the commit to use
        compile 'com.github.Evisceration:MaterialPreferences:88ed97f7a4'
    } else {
        compile project(':locallibs:MaterialPreferences:library')
    }
}
